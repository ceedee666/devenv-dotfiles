#!/bin/zsh

set -e

copy_data () {
  local id=$1
  local login=$2
  local sessionkey=$3
  local totp

  echo "Name: $(printf "%s" "$login" | jq -r ".name")"

  # Copy the username to the clipboard
  echo "> Copying Username"
  printf "%s" "$login" | jq -r ".login.username" | pbcopy

  # Wait for user input before coping the password
  echo "> Press any key to copy password..."
  read

  # Copy the password to the clipboard
  echo "> Copying Password"
  printf "%s" "$login" | jq -r ".login.password" | pbcopy

  # Copy a TOTP Token if available
  totp=$(printf "%s" "$login" | jq -r ".login.totp")

  if [[ $totp != "null" ]]; then
    echo "> Copying TOTP Token"
    bw get totp $id --session $sessionkey | pbcopy
  fi
}

main() {
  local searchterm=$1
  local sessionkey logins login id

  #Unlock the vault an store the session key
  sessionkey=$(bw unlock --raw)

  # Search for passwords using the search term
  logins=$(bw list items --search $searchterm --session $sessionkey)

  id=$(printf "%s" "$logins" \
    | jq -r '.[] | "\(.name)\t\(.login.username)\t\(.id)"' \
    | fzf --reverse --with-nth=1,2 --delimiter="\t" --select-1 --exit-0 \
    | awk -F"\t" '{print $3}'
  )

  if [[ -n $id ]]; then
    login="$(printf "%s" "$logins" | jq ".[] | select(.id == \"$id\")")"
    copy_data $id $login $sessionkey
  fi
}

main "$@"
